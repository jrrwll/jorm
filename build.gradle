repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

apply plugin: 'java-library'
java.sourceCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs.add '-parameters'
compileTestJava.options.compilerArgs.add '-parameters'

version = VERSION
group = GROUP_ID

ext {
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    slf4jVersion = "1.7.36"
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    lombokVersion = "1.18.24"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    junitVersion = "5.9.2"
    // https://mvnrepository.com/artifact/org.jooq/jooq
    jooqVersion = '3.17.7'
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // or gradle.properties: org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk/Contents/Home
    // implementation files(Jvm.current().toolsJar)
    implementation 'org.dreamcat:common-core:0.3.1'

    testImplementation "org.jooq:jooq:$jooqVersion"
    testImplementation "org.jooq:jooq-meta:$jooqVersion"
    testImplementation "org.jooq:jooq-codegen:$jooqVersion"

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    testImplementation 'org.xerial:sqlite-jdbc:3.40.0.0'
}

task printVersion {
    doLast {
        print version
    }
}

test {
    useJUnitPlatform()
}

apply from: "./publish.gradle"
apply from: "./jooq.gradle"
